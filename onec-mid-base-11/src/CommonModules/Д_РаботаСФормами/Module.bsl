#Область ПрограммныйИнтерфейс
// Добавляет поля КонтактноеЛицо и СогласованнаяСкидка на форму документа.
//Параметры:
//  Форма - Форма - Форма документа, в который добавляется поле.
//
Процедура ДополнитьФорму(Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы ="Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьПолеСогласованнаяСкидкаИКнопкуПрименитьСкидкуГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы ="Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы ="Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы ="Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередЭлементом(Форма, Форма.Элементы.Основание); 
	ИначеЕсли ИмяФормы ="Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередЭлементом(Форма, Форма.Элементы.СуммаДокумента);
	КонецЕсли;
	
КонецПроцедуры	 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"),Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо";
	
КонецПроцедуры	     

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередЭлементом(Форма, Элемент)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), ,Элемент);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.КонтактноеЛицо";
	
КонецПроцедуры	 

Процедура ДобавитьПолеСогласованнаяСкидкаИКнопкуПрименитьСкидкуГруппаШапкаЛево(Форма)
	
	ГруппаСогласованнаяСкидка = Форма.Элементы.Добавить("ГруппаСогласованнаяСкидка", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);  
    ГруппаСогласованнаяСкидка.Доступность = ИСТИНА;
	ГруппаСогласованнаяСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;

	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"),Форма.Элементы.ГруппаСогласованнаяСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Д_Д_СогласованнаяСкидкаПриИзмененииПосле");
	
	Кмд = Форма.Команды.Добавить("Д_ПрименитьСкидку");
	Кмд.Действие = "Д_ПрименитьСкидку";
	Кмд.Заголовок = "Применить скидку";
	
	Кнопка = Форма.Элементы.Добавить("ПрименитьСкидку", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСогласованнаяСкидка);
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	Кнопка.ИмяКоманды = "Д_ПрименитьСкидку";

КонецПроцедуры	
#КонецОбласти
